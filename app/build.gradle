apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdk 34

    defaultConfig {
        applicationId 'com.github.tvbox.osc'
        minSdk 23
        targetSdk 34
        versionCode 30
        versionName '1.1.0'
        multiDexEnabled true
        //设置room的Schema的位置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        ndk {
            //abiFilters "armeabi-v7a"//, ""
            abiFilters "arm64-v8a"
        }

    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/beans.xml']
        }
    }


    // 执行配置
    applicationVariants.configureEach { variant ->

        // Apk 输出配置
        variant.outputs.configureEach { output ->
            outputFileName = 'MBox_v' + variant.versionName + '_' + variant.buildType.name
            if (variant.buildType.name == buildTypes.release.getName()) {
                outputFileName += '_' + new Date().format("yyyyMMdd")
            }
            outputFileName += '.apk'
        }
    }


    signingConfigs {
        config {
            keyAlias 'key0'
            keyPassword '123456'
            storeFile file('test.jks')
            storePassword '123456'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }


    buildTypes {
        debug {
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    dexOptions {
        javaMaxHeapSize "4g"
        additionalParameters += '--multi-dex'
        additionalParameters += '--set-max-idx-number=48000'
        additionalParameters += '--minimal-main-dex'
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'com.github.tvbox.osc'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }

}

dependencies {
    api fileTree(dir: "libs", include: ['*.jar', '*.aar'])

    implementation libs.material
    implementation libs.core.ktx
    implementation libs.kotlin.stdlib
    implementation libs.kotlin.stdlib.jdk8

    implementation libs.nanohttpd
    implementation libs.core
    implementation libs.appcompat
    implementation libs.constraintlayout
    implementation libs.lifecycle.extensions
    implementation libs.recyclerview
    implementation libs.conscrypt.android

    implementation 'com.squareup.okhttp3:okhttp:3.12.11'
    annotationProcessor libs.room.compiler
    implementation libs.room.runtime
    implementation libs.multidex
    implementation libs.okio
    implementation libs.recycler.help
    implementation libs.loadsir
    implementation libs.gson
    implementation libs.picasso
    implementation libs.autosize
    implementation(libs.xstream) {
        exclude group: 'xmlpull', module: 'xmlpull'
    }
    implementation libs.eventbus
    implementation libs.hawk
    implementation libs.android.retrofuture

    implementation(libs.xwalk.shared.library) {
        exclude group: 'com.android.support'
    }
    implementation libs.okgo
    implementation libs.tv.recyclerview

    implementation libs.xxpermissions
    implementation libs.jsoup
    implementation libs.android.file.chooser
    implementation libs.commons.io
    implementation libs.juniversalchardet

    //沉浸式状态栏
    implementation libs.immersionbar
    //标题栏
    implementation libs.titlebar
    //弹窗
    implementation libs.xpopup
    //工具类
    implementation libs.utilcodex
    //阴影/shape/选择器/圆角/...
    implementation libs.shadowLayout
    //glide图片加载
    implementation libs.glide
    annotationProcessor libs.compiler
    //展开收缩tv
    implementation libs.expandableTextView
    //lottie动画
    implementation libs.lottie
    //流式布局
    implementation libs.flowlayout.lib
    //崩溃
    implementation project(":crash")
    //dlna投屏
    implementation libs.dlna.dmc
    implementation libs.viewpager2

    implementation files('libs/thunder.jar')
    implementation files('libs/commons-lang3-3.12.0.jar')
    implementation project(":player")
    implementation project(":quickjs")
}